plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.2'
    id 'idea'
    id 'application'
    id 'signing'
    id 'maven-publish'
    id 'visual-studio'
}
group 'com.nikhilm'
sourceCompatibility = 1.11
def grpcVersion = '1.53.0'
def baseVersion = '3.9.4'
def baseGroupId = 'io.github.adempiere'
def javaMainClass = "org.spin.server.AllInOneServices"
repositories {
    mavenCentral()
}
dependencies {
    implementation fileTree(
        dir: 'dependencies',
        include: [
            '*.jar'
        ]
    )
	implementation "io.netty:netty-handler:4.1.89.Final"
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
	implementation 'com.sun.xml.bind:jaxb-impl:3.0.0-M4'
	implementation 'com.sun.xml.bind:jaxb-core:3.0.0-M4'
	implementation 'javax.activation:activation:1.1.1'
	//	ADempiere Core
	implementation "${baseGroupId}:base:${baseVersion}"
	implementation "${baseGroupId}:point-of-sales:${baseVersion}"
	implementation "${baseGroupId}:store:${baseVersion}"
	implementation "${baseGroupId}:distribution:${baseVersion}"
	implementation "${baseGroupId}:freight:${baseVersion}"
	implementation "${baseGroupId}:store:${baseVersion}"
	implementation "${baseGroupId}:human-resource-and-payroll:${baseVersion}"
	implementation "${baseGroupId}:extend:${baseVersion}"
	implementation "${baseGroupId}:migration:${baseVersion}"
	implementation "${baseGroupId}:asset:${baseVersion}"
	implementation "${baseGroupId}:crm:${baseVersion}"
	implementation "${baseGroupId}:production:${baseVersion}"
	implementation "${baseGroupId}:project:${baseVersion}"
	implementation "${baseGroupId}:request:${baseVersion}"
	implementation "${baseGroupId}:adempiere.test:${baseVersion}"
	implementation "${baseGroupId}:cashflow:${baseVersion}"
	implementation "${baseGroupId}:manufacturing:${baseVersion}"
	implementation "${baseGroupId}:wms:${baseVersion}"
	implementation "${baseGroupId}:openid-authentication:${baseVersion}"
	implementation "${baseGroupId}:investment-and-loan:${baseVersion}"
	implementation "${baseGroupId}:time-and-attendance:${baseVersion}"
	implementation "${baseGroupId}:loan:${baseVersion}"
	implementation "${baseGroupId}:discord-notifier:${baseVersion}"
	implementation "${baseGroupId}:telegram-notifier:${baseVersion}"
	implementation "${baseGroupId}:tools:${baseVersion}"
	implementation "${baseGroupId}:jasperreports:${baseVersion}"
	//	Others
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
	// ADempiere External Libraries
	implementation 'com.github.jjYBdx4IL:ecs:1.4.2.1'
}

configurations.all {
	resolutionStrategy {
		// used by aws-java-sdk-core (storefront is joda-time-2.10.4)
		force("joda-time:joda-time:2.8.1")
	}
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.12.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.34.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'src/main/proto'
        }
        resources {
    		srcDirs 'src'
        	include 'org/adempiere/legacy/apache/ecs/ecs.properties'
    	}
    }
}

startScripts.enabled = false

run {
	doFirst {
		// Set environment variables
		environment 'ADEMPIERE_APPS_TYPE', 'wildfly'
	}
	args = [
		"resources/standalone.yaml"
	]
}

application {
	// mainClass = javaMainClass
	mainClass.set(javaMainClass)
}
jar {
	manifest {
		attributes(
			'Main-Class': javaMainClass
		)
	}
}

task ADempiereAllInOne(type: CreateStartScripts) {
	mainClass = javaMainClass
    applicationName = 'adempiere-all-in-one-server'
    defaultJvmOpts = Arrays.asList("-DPropertyFile=Adempiere.properties", "-Dorg.adempiere.server.embedded=true")
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into('bin') {
	from(ADempiereAllInOne)
    fileMode = 0755
}

//	Create release for project
task createRelease(type: Copy) {
   dependsOn build
   from file("$buildDir/distributions/")
   into file("$buildDir/release/")
   doLast {
        file('build/release/')
        	.listFiles({file -> file.isFile()} as FileFilter).sort()
        	.each { File file ->
            ant.checksum file: file
        }
   	}
}

def libraryGroupId = 'io.github.adempiere'
version = System.getenv("LIBRARY_VERSION")

jar {
    manifest {
        attributes("Implementation-Title": "Adempiere Backend Server",
                   "Implementation-Version": version)
    }
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId libraryGroupId
            artifactId 'backend'
            version
           	from components.java
           	pom {
                name = 'backend'
                description = 'ADempiere Backend Service'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/backend.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/backend.git'
                    url = 'http://github.com/adempiere/backend'
                }
            }
		}
	}
}

signing {
	def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}