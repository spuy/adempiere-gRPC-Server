apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    }
    dependencies { // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.11' }
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenLocal()
    jcenter()

    flatDir {
        dirs '../adempiere/install/build/spuy-core/build/out'
        dirs '../adempiere/install/build/spuy-core/lib'
        dirs '../adempiere/install/build/Adempiere/lib'
        dirs '../adempiere/install/build/Adempiere/packages/ADempierePOS/libs'
        dirs '../adempiere/install/build/Adempiere/packages/CashflowManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/FinanceManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/FixedAssetManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/FleetManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/FreightManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/HumanResourceAndPayroll/libs'
        dirs '../adempiere/install/build/Adempiere/packages/LoanManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/ManufacturingManagement/libs'
        dirs '../adempiere/install/build/Adempiere/packages/ProductionLight/libs'
        dirs '../adempiere/install/build/Adempiere/packages/StoreIntegration/libs'
        dirs '../adempiere/install/build/Adempiere/packages/TimeAndAttendance/libs'
        dirs '../adempiere/install/build/Adempiere/packages/WarehouseManagement/libs'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
def libraryVersion = '1.3.0'
// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.17.0' // CURRENT_GRPC_VERSION
def nettyTcNativeVersion = '2.0.17.Final'
def protobufVersion = '3.5.1'
def protocVersion = '3.5.1-1'

dependencies {
    implementation fileTree(dir: 'dependences', include: ['*.jar'])
	implementation "com.google.api.grpc:proto-google-common-protos:1.0.0"
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly "javax.annotation:javax.annotation-api:1.2"

    // Used in HelloWorldServerTls
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.9.5"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0'
	implementation 'com.itextpdf:itext-pdfa:5.5.2'
	implementation 'com.lowagie:itext:2.1.7'


    implementation fileTree(dir: '../spuy-core/build/out', include: '*.jar')
    implementation fileTree(dir: '../spuy-core/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/ADempierePOS/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/CashflowManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/FinanceManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/FixedAssetManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/FleetManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/FreightManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/HumanResourceAndPayroll/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/LoanManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/ManufacturingManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/ProductionLight/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/StoreIntegration/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/TimeAndAttendance/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/install/build/Adempiere/packages/WarehouseManagement/lib', include: '*.jar')
    implementation fileTree(dir: '../adempiere/zkwebui/WEB-INF/lib', include: '*.jar')
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'src/main/proto'
            srcDirs '../spuy-core/base/src'
            srcDirs '../spuy-core/client/src'
            srcDirs '../spuy-core/org.openup.luy/src/main/java/base'
        }
    }
}

// Generate IntelliJ IDEA's .idea & .iml project files
apply plugin: 'idea'

// Provide convenience executables for trying out the examples.
apply plugin: 'application'

startScripts.enabled = false

application {
    mainClassName = 'org.spin.server.AllInOneServices'
}

task ADempiereAllInOne(type: CreateStartScripts) {
    mainClassName = 'org.spin.server.AllInOneServices'
    applicationName = 'adempiere-all-in-one-server'
    defaultJvmOpts = Arrays.asList("-DPropertyFile=Adempiere.properties", "-Dorg.adempiere.server.embedded=true")
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into('bin') {
	from(ADempiereAllInOne)
    fileMode = 0755
}

//	Create release for project
task createRelease(type: Copy) {
   dependsOn build
   from file("$buildDir/distributions/")
   into file("$buildDir/release/")
   //destinationDir(file('build/release/'))
   doLast {
        file('build/release/')
        	.listFiles({file -> file.isFile()} as FileFilter).sort()
        	.each { File file ->
            ant.checksum file: file
        }
   	}
}

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/erpcya/Repository"
            credentials {
                username = System.properties['deploy.user'] ?: System.getenv("GITHUB_DEPLOY_USER")
                password = System.properties['deploy.token'] ?: System.getenv("GITHUB_DEPLOY_TOKEN")
            }
        }
    }
    publications {
		mavenJava(MavenPublication) {
        	groupId 'org.erpya.adempiere.grpc'
            artifactId 'adempiere-backend-grpc-server'
            version libraryVersion
           	from components.java
           	pom {
                name = 'gRPC Server for ADempiere'
                description = 'A gRPC Server based on ADempiere Persistence Object'
                url = 'https://www.erpya.com/'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
            }
		}
	}
}
